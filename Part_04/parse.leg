# The leg parser for the alic language.
# (c) 2025 Warren Toomey, GPL3

%{
#include <stdio.h>
#include <stdlib.h>
#include "alic.h"
#include "proto.h"

#define YYSTYPE ASTnode *

extern uint Line;
extern Type *ty_void;
extern Type *ty_bool;
extern Type *ty_int8;
extern Type *ty_int16;
extern Type *ty_int32;
extern Type *ty_int64;
extern Type *ty_uint8;
extern Type *ty_uint16;
extern Type *ty_uint32;
extern Type *ty_uint64;
extern Type *ty_flt32;
extern Type *ty_flt64;

extern FILE *debugfh;
%}

## The Input File
#
input_file = function_declaration_list EOF

## Function Declarations
#
function_declaration_list = function_declaration function_declaration_list
	| function_declaration

function_declaration = f:function_prototype
	{
	  declare_function(f);
	}
	s:statement_block
	{
	  gen_func_statement_block(s);
	}
	| f:function_prototype SEMI
	{
	  // Add the function declaration to the symbol table
	  add_function(f, f->left);
	}

function_prototype = func:typed_declaration
		LPAREN params:typed_declaration_list RPAREN
	{
	  func->left= params;
	  $$ = func;
	}

typed_declaration = t:type s:SYMBOL
	{
	  // Add the type to the IDENT node
	  s->type= t->type;
	  $$ = s;
	}

typed_declaration_list = d:typed_declaration COMMA dlist:typed_declaration_list
				{ d->mid= dlist; $$ = d; }
	| d:typed_declaration	{ $$ = d; }
	| VOID			{ $$ = NULL; }

## Statements: a statement block has all the declarations first,
#  followed by at least one procedural statement.
#
statement_block = LCURLY s:procedural_stmts RCURLY
	{ $$ = s; }
	| LCURLY d:declaration_stmts s:procedural_stmts RCURLY
	{ d->right= s; $$ = d; }

# One or more declaration statements
declaration_stmts = d:declaration_stmt dlist:declaration_stmts
	{ d->mid= dlist; $$ = d; }
	| d:declaration_stmt
	{ $$ = d; }

declaration_stmt = s:typed_declaration ASSIGN e:expression SEMI
	{ $$= declaration_statement(s, e); }

procedural_stmts = l:procedural_stmt r:procedural_stmts
	{
	  // Glue left and right together if there are both
	  $$ = mkastnode(A_GLUE,l,NULL,r);
	}
	| l:procedural_stmt
	{
	  // Otherwise just return left if there is no right
	  $$ = l;
	}

procedural_stmt = print_stmt
	| assign_stmt
	| if_stmt
	| while_stmt
	| for_stmt
	| function_call

print_stmt = PRINTF LPAREN s:STRLIT COMMA e:expression RPAREN SEMI
	{ $$ = print_statement(s, e); }

assign_stmt = for_assign_stmt SEMI

for_assign_stmt = v:variable ASSIGN e:expression
	{ $$ = assignment_statement(v, e); }

if_stmt = IF LPAREN e:relational_expression RPAREN t:statement_block
					    ELSE   f:statement_block
	{ $$= mkastnode(A_IF, e, t, f); }
	| IF LPAREN e:relational_expression RPAREN t:statement_block
	{ $$= mkastnode(A_IF, e, t, NULL); }

while_stmt = WHILE LPAREN e:relational_expression RPAREN s:statement_block
	{ $$= mkastnode(A_WHILE, e, s, NULL); }

for_stmt = FOR LPAREN i:assign_stmt e:relational_expression SEMI
		send:for_assign_stmt RPAREN s:statement_block
	{
	  // Glue the end code after the statement block
	  s = mkastnode(A_GLUE,s,NULL,send);
	  // We put the initial code at the end so that
	  // we can send the node to gen_WHILE() :-)
	  $$= mkastnode(A_FOR, e, s, i);
	}

# Function Calls: no parameters or a list of parameters
#
function_call =
	s:SYMBOL LPAREN e:expression_list RPAREN SEMI
	{ $$ = mkastnode(A_FUNCCALL,s,NULL,e); }
	| s:SYMBOL LPAREN RPAREN SEMI
	{ $$ = mkastnode(A_FUNCCALL,s,NULL,NULL); }

expression_list =
	e:expression COMMA l:expression_list
	{ $$ = mkastnode(A_GLUE,e,NULL,l); }
	| e:expression		
	{ $$ = mkastnode(A_GLUE,e,NULL,NULL); }

# Expressions
#
expression = bitwise_expression

bitwise_expression = 
	( INVERT l:relational_expression	{ l  = unarop(l, A_INVERT); }
	| l:relational_expression
	)
	( AND r:relational_expression		{ l  = binop(l,r,A_AND); }
	| OR  r:relational_expression		{ l  = binop(l,r,A_OR);  }
	| XOR r:relational_expression		{ l  = binop(l,r,A_XOR); }
	)* -					{ $$ = l; }

relational_expression =
	( NOT l:shift_expression		{ l  = unarop(l, A_NOT); }
	| l:shift_expression
	)
	( GE r:shift_expression			{ l  = binop(l,r,A_GE); }
	| GT r:shift_expression			{ l  = binop(l,r,A_GT); }
	| LE r:shift_expression			{ l  = binop(l,r,A_LE); }
	| LT r:shift_expression			{ l  = binop(l,r,A_LT); }
	| EQ r:shift_expression			{ l  = binop(l,r,A_EQ); }
	| NE r:shift_expression			{ l  = binop(l,r,A_NE); }
	)* -					{ $$ = l; }

shift_expression = l:additive_expression
	( LSHIFT r:additive_expression		{ l  = binop(l,r,A_LSHIFT); }
	| RSHIFT r:additive_expression		{ l  = binop(l,r,A_RSHIFT); }
	)* -					{ $$ = l; }

additive_expression =
	( PLUS? l:multiplicative_expression
        | MINUS l:multiplicative_expression	{ l  = unarop(l, A_NEGATE); }
	)
        ( PLUS  r:multiplicative_expression	{ l  = binop(l,r,A_ADD); }
        | MINUS r:multiplicative_expression	{ l  = binop(l,r,A_SUBTRACT); }
        )* -					{ $$ = l; }

multiplicative_expression = l:factor
	( STAR  r:factor 			{ l  = binop(l,r,A_MULTIPLY); }
	| SLASH r:factor 			{ l  = binop(l,r,A_DIVIDE); }
	)* -					{ $$ = l; }

factor =  f:FLTLIT 	{ $$ = f; }
	| f:INTLIT	{ $$ = f; }
	| TRUE		{ $$ = mkastleaf(A_NUMLIT, ty_bool, true, NULL, 1); }
	| FALSE		{ $$ = mkastleaf(A_NUMLIT, ty_bool, true, NULL, 0); }
	| v:variable	{ $$ = v; }

# Types
#
type =    "int8"   -	{ $$= mkastleaf(A_TYPE, ty_int8,  false, NULL,0);  }
	| "int16"  -	{ $$= mkastleaf(A_TYPE, ty_int16,  false, NULL,0); }
	| "int32"  -	{ $$= mkastleaf(A_TYPE, ty_int32,  false, NULL,0); }
	| "int64"  -	{ $$= mkastleaf(A_TYPE, ty_int64,  false, NULL,0); }
	| "uint8"  -	{ $$= mkastleaf(A_TYPE, ty_uint8,  false, NULL,0); }
	| "uint16" -	{ $$= mkastleaf(A_TYPE, ty_uint16, false, NULL,0); }
	| "uint32" -	{ $$= mkastleaf(A_TYPE, ty_uint32, false, NULL,0); }
	| "uint64" -	{ $$= mkastleaf(A_TYPE, ty_uint64, false, NULL,0); }
	| "flt32"  -	{ $$= mkastleaf(A_TYPE, ty_flt32,  false, NULL,0); }
	| "flt64"  -	{ $$= mkastleaf(A_TYPE, ty_flt64,  false, NULL,0); }
	| "bool"   -	{ $$= mkastleaf(A_TYPE, ty_bool,   false, NULL,0); }
	| VOID		{ $$= mkastleaf(A_TYPE, ty_void,   false, NULL,0); }

# Variables, Symbols, Literals
#
variable = s:SYMBOL	{ $$ = mkident(s); }

SYMBOL = < [A-Za-z_]+ [A-Za-z0-9_]* > -
	{
	  ASTnode *n= mkastleaf(A_IDENT, NULL, false, NULL, 0);
	  n->strlit= strdup(yytext);
	  $$ = n;
	}

FLTLIT = < '-'? DIGIT+ '.' DIGIT+ > -
	{ Type *littype;
	  uint64_t val= parse_litval(yytext, &littype);
	  $$ = mkastleaf(A_NUMLIT, littype, true, NULL, val);
	}

INTLIT = < '-'? DIGIT+ > -
	{ Type *littype;
	  uint64_t val= parse_litval(yytext, &littype);
	  $$ = mkastleaf(A_NUMLIT, littype, true, NULL, val);
	}

DIGIT = [0-9]

STRLIT = '"' < [^\"]* > '"' -
	{
	  ASTnode *n= mkastleaf(A_STRLIT, NULL, false, NULL, 0);
	  n->strlit= strdup(yytext);
	  $$ = n;
	}

# Operators
#
SLASH  = '/'  -
STAR   = '*'  -
MINUS  = '-'  -
PLUS   = '+'  -
ASSIGN = '='  -
GT     = '>'  -
GE     = '>=' -
LT     = '<'  -
LE     = '<=' -
EQ     = '==' -
NE     = '!=' -
INVERT = '~'  -
NOT    = '!'  -
AND    = '&'  -
OR     = '|'  -
XOR    = '^'  -
LSHIFT = '<<' -
RSHIFT = '>>' -

# Syntactic Sugar
#
SEMI   = ';'	-
LCURLY = '{'	-
RCURLY = '}'	-
LPAREN = '('	-
RPAREN = ')'	-
COMMA  = ','	-

# Keywords
#
PRINTF = "printf" -
TRUE   = 'true'   -
FALSE  = 'false'  -
IF     = 'if'     -
ELSE   = 'else'   -
WHILE  = 'while'  -
FOR    = 'for'    -
VOID   = 'void'   -

# Whitespace and End of File
#
EOF = !.
CR  = ( '\n' | '\r' | '\r\n' )		{ Line++; }
-   = ( CR   | ' '  | '\t'   )*
